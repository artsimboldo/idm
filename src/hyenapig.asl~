// Agent hyenapig in project iDM.mas2j

/* Initial beliefs and rules */

hungry :- life(X) & X<50.

/* Initial goals */

!roam.

/* Plans */

@l1
+life(0)
	<-	.print("AAAARGGHHH!!!");
		.my_name(X);
		.kill_agent(X).

@l2
// use life() as a trigger because constantly changing 
// instead of food(_,_) which is never changing
+life(_) : hungry & food(X,Y) & not searchingfood
	<-	//.print("searching for food");
		.drop_all_intentions;			// then stop ongoing activites right now
		?at(X1,Y1);
		if (X1 \== X | Y1 \== Y)
		{
			find_path(X,Y)					// and plan a path to food
		};
		+searchingfood;
		!at(X,Y).						// and run to it

@r1
+!roam : not goingto(_,_)
	<-	//.print("is searching for a new destination...");
		find_path(-1,-1);
		!!roam.

@r2
+!roam : goingto(X,Y)
	<-	//.print("is ongoing to location (", X, ",", Y, ")");
		!at(X,Y).
	
@a1
+!at(X,Y) : not at(X,Y)
	<-	move_on_path;
		.wait(200);
		!!at(X,Y).
		
@a2
+!at(X,Y) : at(X,Y) & not searchingfood
	<-	//.print("is arrived...");
		.wait(1000);
		!roam.

@a3
+!at(X,Y) : at(X,Y)	& searchingfood
	<-	//.print("is arrived and eats food,");
		-searchingfood;
		eat(food);
		.wait(3000);
		!roam.

